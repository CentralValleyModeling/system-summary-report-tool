plugins {
    id 'ssrt.deps-conventions'
    id 'ssrt.java-conventions'
    id 'ssrt.publishing-conventions'
    id 'application'
    id 'distribution'
}

dependencies {
    implementation(project(":system-summary-report-tool"))
    runtimeOnly(libs.javaHeclib){artifact {type = 'zip'}}
}

task extractDll(type: Copy) {
    def zipFile = configurations.runtimeClasspath.find { it.name.endsWith('.zip') }
    from zipTree(zipFile)
    into "$buildDir/dll"
}

startScripts {
    applicationName = 'system-summary-report-tool'
    doLast {
        unixScript.text = "# Unix version not available"
        windowsScript.text = windowsScript.text.replace('set DEFAULT_JVM_OPTS=', 'set DEFAULT_JVM_OPTS=-Djava.library.path=%~dp0\\..\\dll')
    }
}

mainClassName = 'gov.ca.dwr.callite.Main'

tasks.named('distTar').configure { enabled = false }
distributions {
    main {
        contents {
            into('dll') {
                from "$buildDir/dll"
            }
        }
    }
}

tasks.withType(Jar).configureEach {
    enabled = false
}

distZip.dependsOn extractDll
distTar.dependsOn extractDll